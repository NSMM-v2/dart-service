plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.nsmm.esg'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    // Spring Boot 기본
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // Kafka
    implementation 'org.springframework.kafka:spring-kafka'

    // 데이터베이스
    implementation 'com.mysql:mysql-connector-j'

    // Lombok (중복 제거)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // 문서화 (중복 제거)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    // Jackson (XML, JSON 처리)
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // 캐싱 (중복 제거 및 버전 통일)
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Zip 처리
    implementation 'net.lingala.zip4j:zip4j:2.11.5'
    
    // Resilience4j
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'
    implementation 'io.github.resilience4j:resilience4j-ratelimiter:2.2.0'
    
    // 로깅
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    
    // 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // 테스트용 Lombok
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('bootJar') {
    archiveFileName = "dart-api-service.jar"
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/html')
    }
    sourceDirectories.setFrom(files("src/main/java"))
    classDirectories.setFrom(files(sourceSets.main.output.classesDirs.collect {
        fileTree(dir: it, exclude: [
            "**/dto/**",
            "**/entity/**",
            "**/config/**",
            "**/*Application*",
            "**/common/**"
        ])
    }))
}

// 애플리케이션 실행 시 항상 dev 프로파일을 기본으로 사용하도록 설정
bootRun {
    args = ['--spring.profiles.active=dev']
}
